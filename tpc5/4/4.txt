
1 - Lista de todos os pokemons com os seguintes atributos: número, nome em inglês e tipo


select ?numero ?nome (GROUP_CONCAT(distinct ?tipo ; separator=" | ") as ?tipos) where {
    ?s a :Pokemon .
    ?s :Name ?nome .
    FILTER (lang(?nome) = 'en') .
    ?s :Number ?numero .
    ?s :HasType ?type .
    ?type :Name ?tipo .
    FILTER (lang(?tipo) = 'en') .
} 
group by ?numero ?nome 
order by ?numero

2 - Quantos pokémons estão catalogadas na ontologia?

select (count(?s) as ?total) where {
    ?s a :Pokemon .
} 

3 - Quantas evoluções tem o pokémon com número 5?

select (count(*) as ?total)  where {
  { 
    ?p a :Pokemon .
    ?p :Number 5 .
    ?p :EvolvesFrom ?evolution .
    ?evolution :EvolvesTo ?pDepois .
  } # pokemons que evolvem a partir do 5
      UNION
  { 
    ?pAnterior a :Pokemon .
    ?pAnterior :EvolvesFrom ?evolution2 .
    ?evolution2 :EvolvesTo ?p .
    ?p :Number 5 .
  } # pokemons que evoluiram para o 5
}
 

4 - Quais são os pokémons para os quais pode evoluir o pokémon de nome "Eevee"?

select ?pokemon where {
    ?s a :Pokemon .
    ?s :Name 'Eevee'@en .
    ?s :EvolvesFrom ?evolution .
    ?evolution :EvolvesTo ?poke .
    ?poke a :Pokemon .
    ?poke :Name ?pokemon .
    FILTER (lang(?pokemon) = 'en') .
} 

5 - Quantos tipos diferentes de pokémons exitem na ontologia?

select (count(distinct ?tipo) as ?numTipos) where {
    ?s a :Pokemon .
    ?s :HasType ?tipo .
}

6 - Quantos pokémons existem associados a cada tipo?

select ?tipo (count(?s) as ?numPokemons) where {
    ?s a :Pokemon .
    ?s :HasType ?t .
    ?t :Name ?tipo .
    FILTER (lang(?tipo) = 'en') .
} 
group by ?tipo 